// Copyright 2018 The Chromium Authors
// Use of this source code is governed by a BSD-style license that can be
// found in the LICENSE file.

apply plugin: ChromiumPlugin

repositories {
    google()
    mavenCentral()
}

dependencies {
    // Note about the configuration names: they are defined in buildSrc/ChromiumPlugin

    // Needed only by internal targets:
    compile 'com.android.support:support-annotations:28.0.0'

    // buildCompile needed because we add a custom dep of onto this from guava
    // to fix "unknown enum constant ReflectionSupport$Level.FULL".
    buildCompile 'com.google.j2objc:j2objc-annotations:2.8'

    // Needed by androidx.room, but by default it selects a lower version that
    // does not exist in CIPD.
    compile 'org.jetbrains.kotlinx:atomicfu-jvm:0.23.2'
    // Needed by androidx and by doubledown targets.
    // Note: These have version overrides set in ChromiumDepGraph.groovy.
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-guava:1.8.1"
    compile "org.jetbrains.kotlinx:kotlinx-serialization-core-jvm:1.7.2"
    compile "org.jetbrains.kotlinx:kotlinx-serialization-json:1.7.2"
    compile "org.jetbrains.kotlin:kotlin-parcelize-runtime:1.9.22"

    // Used by ModuleInterfaceProcessor.java
    buildCompile 'com.squareup:javapoet:1.13.0'

    // Used by resource shrinking.
    // buildCompileNoDeps targets do not bring in any of their dependencies,
    // this list of targets is carefully curated for the use of the resources
    // shrinker. If these are needed for something other than the resources
    // shrinker, use buildCompile instead.
    String androidToolsVersion = '30.2.0-beta01'
    buildCompileNoDeps "com.android.tools:sdk-common:${androidToolsVersion}"
    buildCompileNoDeps "com.android.tools:common:${androidToolsVersion}"
    buildCompileNoDeps "com.android.tools.layoutlib:layoutlib-api:${androidToolsVersion}"

    androidTestCompile 'com.google.android.apps.common.testing.accessibility.framework:accessibility-test-framework:4.0.0'
    // accessibility-test-framework:3.1.2 depends on jsoup 1.12, which has a security
    // vulnerability, so grab a later version.
    androidTestCompile 'org.jsoup:jsoup:1.14.3'
    androidTestCompile 'com.googlecode.java-diff-utils:diffutils:1.3.0'
    // Version 1.2 is needed by espresso-web, but we'll newer 1.2.1.
    androidTestCompile 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'

    // accessibility-test-framework has a phantom dependency on the really old
    // protobuf runtime which trips our asserts. Manually add dep here but
    // exclude it in ChromiumDepGraph.
    androidTestCompile 'com.google.protobuf:protobuf-lite:3.0.0'

    String mockitoVersion = '5.19.0'
    androidTestCompile "org.mockito:mockito-android:${mockitoVersion}"
    androidTestCompile "org.mockito:mockito-core:${mockitoVersion}"
    androidTestCompile "org.mockito:mockito-subclass:${mockitoVersion}"

    // Used by //third_party/byte_buddy
    androidTestCompile "net.bytebuddy:byte-buddy:1.14.5"
}

task setUpRepository(type: BuildConfigGenerator) {
    // Paths are relative to the chromium source root.
    pathToBuildGradle 'third_party/android_deps'
    cipdPackagePrefix 'chromium/third_party/android_deps'
    internalTargetVisibility = [':*', '//third_party/androidx:*']
}
